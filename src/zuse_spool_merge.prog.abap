*&-------------------------------------------------------------------------------------*
*&  Report  ZUSE_SPOOL_MERGE
*&  Purpose              : Utility program triggered by enhancement
*&  in program RPCS0000 (Scheduler for Parallel Execution of Evaluation Programs)
*&  When multiple job splits occur this custom development has been created to merge
*&  the spool files from the same main job into the one spool report where the
*&  spool report title matches and the report title has been activiated in
*&  configuration table ZHR_RPCS0000_A
*&-------------------------------------------------------------------------------------*
*& Modification Logs    :
*-----------------------------------------------------------------------------------------------------*
*Mod  |Date       |User ID       |Description                       |Change Label  |Workbench Request *
*-----------------------------------------------------------------------------------------------------*
*-----------------------------------------------------------------------------------------------------*
REPORT  ZUSE_SPOOL_MERGE LINE-SIZE 1023 NO STANDARD PAGE HEADING.    .

INCLUDE zuseihr_spool_merge_top.
INCLUDE zuseihr_spool_merge_forms.

**************************************************************************************
START-OF-SELECTION.
**************************************************************************************
  PERFORM initilation.

  g_completed  = abap_true.
  g_not_locked = abap_true.
*-------------------------------------------------------
* continue only if all job of the job set have completed
* and this merge prorgam is not already running
*-------------------------------------------------------
  PERFORM read_jobs TABLES   gt_tbtco
                    USING    p_jobnam  p_jobcnt p_secs  p_date
                    CHANGING g_completed
                             g_jobnam  .

  CHECK g_completed = abap_true.
  CHECK gt_tbtco[] IS NOT INITIAL.  "jobs found

  PERFORM lock_program USING g_not_locked.
  CHECK g_not_locked = abap_false.

*-------------------------------------------------------
* Continue - All jobs complete,
*           EZHR_RPCS0000_L locked by this execution
*-------------------------------------------------------

* read spool id created by the job name/s
  SELECT spoolid  jobname  jobcount FROM  tbtc_spoolid INTO TABLE gt_spool_job
    FOR ALL ENTRIES IN gt_tbtco
          WHERE  jobname   = gt_tbtco-jobname
            AND  jobcount  = gt_tbtco-jobcount.
* format spool number
  LOOP AT gt_spool_job ASSIGNING FIELD-SYMBOL(<fs_spool>).
    <fs_spool>-rqident = <fs_spool>-spoolid.
  ENDLOOP.
* read spool titles
  SELECT rqident, rq1name, rq2name, rqtitle FROM tsp01 FOR ALL ENTRIES IN @gt_spool_job
        WHERE rqident = @gt_spool_job-rqident
          AND rqclient = @sy-mandt
          AND rqtitle IN @s_title
  INTO CORRESPONDING FIELDS OF TABLE @gt_spool.
* exclude spools generated by this custom report
  DELETE gt_spool WHERE  rq2name+0(8) = sy-repid+0(8).

**************************************************************************************
END-OF-SELECTION.
**************************************************************************************
  IF gt_tbtco[] IS INITIAL.
    MESSAGE i016(zhrau_rpt) ."No jobs / spool reports selected to merge'.
  ELSE.
    IF  g_not_locked = abap_true.
      MESSAGE i013(zhrau_rpt) ."Not processed as other jobs active'.
    ELSE.
      IF gt_spool[] IS INITIAL.
        MESSAGE i016(zhrau_rpt) ."No jobs / spool reports selected to merge'.
      ELSE.
        PERFORM write_reports.
        MESSAGE i014(zhrau_rpt) ."Merge reports complete'.
      ENDIF.
    ENDIF.
  ENDIF.
  PERFORM unlock_program  USING g_not_locked.
